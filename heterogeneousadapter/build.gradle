apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.marverenic.heterogeneousadapter'
version = '1.0'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:recyclerview-v7:25.3.1'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'HeterogeneousAdapter'
        name = 'com.marverenic.heterogeneousadapter'

        version {
            name = '1.0'
            released  = new Date()
            vcsTag = 'v1.0'
        }

        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/marverenic/HeterogeneousAdapter'
        websiteUrl = 'https://github.com/marverenic/HeterogeneousAdapter'
    }
    configurations = ['archives']
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}

task generateJavadocsJar(type: Jar) {
    from javadoc.destinationDir
    classifier 'javadoc'
    dependsOn javadoc
}

artifacts {
    archives generateJavadocsJar
    archives generateSourcesJar
}